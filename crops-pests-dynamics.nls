;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; between start of season and date-to-flee

to update-crop-patches-between-start-season-and-date-to-flee
  
  ;;; update predators-presence (check)
  ask patches with [(count adult-predators-here = 0) and (count juvenile-predators-here = 0)][set predator-presence FALSE]
  ask patches with [(count adult-predators-here > 0) or (count juvenile-predators-here > 0)][set predator-presence TRUE]

  ;;; possibility to choose three pattern-infection methods (see below)
  pattern-infection

  ;;; update latent
  ask patches with [land-cover = 1 and state = 1]
  [
    ; update timers
    set time-since-infection time-since-infection + 1
    set time-for-crop-loss time-for-crop-loss + 1
    
    ; update next-state
    if time-since-infection = latent-period-duration [set next-state 2]
  ]

  ;;; update attractive without presence of adult-predators
  ask patches with [land-cover = 1 and state = 2 and predator-presence = FALSE]
  [
    ; update timers
    set time-since-infection time-since-infection + 1 
    set time-for-crop-loss time-for-crop-loss + 1
  ]

  ;;; update attractive with presence of adult-predators: this state occurs during only 1 tick (as soon as one or more adults arrive, next-state -> 3)
  ask patches with [land-cover = 1 and state = 2 and predator-presence = TRUE]
  [
    ; update timers
    set time-since-infection time-since-infection + 1 
    
    ; if more than one adult-predators arrive on the same patch in the same time, choose one-of as the first one arrived
    set first-adult-predator-arrived [who] of one-of adult-predators-here
    
    ; update next-state
    set next-state 3
  ]

  ; update occupied by adults
  ask patches with [land-cover = 1 and state = 3 and predator-presence = TRUE]
  [   
    ; update timers
    set time-since-infection time-since-infection + 1 
    set adult-occupation adult-occupation + 1
    
    ; update next-state
    if adult-occupation = duration-before-eggs [ if random-float 1 <= proba-birth-juvenile-predators [set next-state 4] ]
  ]

  ; update occupied by juveniles
  ask patches with [land-cover = 1 and state = 4]
  [
    ; update timers
    set time-since-infection time-since-infection + 1 
    set juvenile-occupation juvenile-occupation + 1
    
    ; update next-state
    if juvenile-occupation = duration-juvenile-occupation [set next-state 0]
  ]

  ;;; transitions between states
  transitions-between-states-for-crop-patches-between-start-season-and-date-to-flee

  ;;; coloration
  ask patches with [land-cover = 1 and state != 0][set pcolor scale-color blue state 4 1]
  ask patches with [land-cover = 1 and state = 0] [set pcolor yellow]

end

;;;;;;;;;;;; transitions

to transitions-between-states-for-crop-patches-between-start-season-and-date-to-flee
  
  ;;; sane -> latent
  ask patches with [land-cover = 1 and state = 0 and next-state = 1]
  [
    ; update state and assign attributes
    set state 1
    set infection-date date
    set time-since-infection 0
    set latent-period-duration 6 ;+ random 2 ; 6,7,8
    
    ; write distribution of infection events
    write-inf-event-file
    
    ; update outputs 
    update-crop-loss-without-control-for-this-cycle-of-infection
    if nb-cycles-infection-curation = 0 [update-crop-loss-1st-infection]   
  ]

  ;;; latent -> attractive
  ask patches with [land-cover = 1 and state = 1 and next-state = 2] 
  [ set state 2 ]

  ;;; attractive -> occupied by adults
  ask patches with [land-cover = 1 and state = 2 and next-state = 3]
  [
    ; update state and assign attributes
    set state 3
    set date-of-predators-arrival date
    set adult-occupation 0
    set duration-before-eggs 6 ;+ random 2 ; 6,7,8
    
    ; write distribution of predators-arrival events
    write-pred-event-file
  ]

  ;;; occupied by adults -> occupied by juveniles
  ask patches with [land-cover = 1 and state = 3 and next-state = 4]
  [
    ; update state and assign attributes
    set state 4
    birth-juvenile-predators
    set juvenile-occupation 0
    set duration-juvenile-occupation 15 ;+ random 6
  ]

  ;;; occupied by juveniles -> sane
  ask patches with [land-cover = 1 and state = 4 and next-state = 0]
  [
    
    ; update crop patches attributes for regulation service    
    update-crop-loss-with-control-for-this-cycle-of-infection
    update-for-this-crop-patch-total-crop-loss-for-a-season
    
        
    ; increment attribute of this patch to count cycles of SIS
    set nb-cycles-infection-curation nb-cycles-infection-curation + 1
    
    ; update state and attributes for a new cycle
    set state 0
    set next-state 0
    
    set predator-presence FALSE
    
    set infection-date FALSE
    set time-since-infection FALSE
    
    set latent-period-duration FALSE
    
    set date-of-predators-arrival FALSE
    set first-adult-predator-arrived FALSE
    set time-for-crop-loss 0

    set duration-before-eggs FALSE
    set juvenile-occupation FALSE
    
    set duration-juvenile-occupation FALSE
    set adult-occupation FALSE
    
    set crop-loss-without-control-for-this-cycle-of-infection 0
    set crop-loss-with-control-for-this-cycle-of-infection 0
   
    ; convert the juvenile turtle into an adult turtle
    ask juvenile-predators-here [die]
    birth-adult-predators
  ]
end

;;;;;;;;;;;; pattern-infection

; beginning of infection in the year: var.?

; assumptions:
; infection depends on:
; exogenous infection = basic rate and spatially randomized
; infection state of the landscape at each tick? = when the number of infected patches increases, we can think that the virulence increases too => regulation by predators is important
; influence of SNH patches as sources of pests? => relation with the number of SNH patches?
; transition between years: is the infection-rate depends on the previous nb of infected patches?

; à chaque tick, on infecte un % du stock de crops : ex. si inf-rate = 4, alors 4% du stock initial de crops est infecté à chaque tick
to pattern-infection
    
  if (ticks mod infection-pattern-frequency) = 0 ; pattern-infection occurs every 3 ticks (not every tick)
  [
    ; each tick, a certain number of sane patches are infected
    ; avec la condition nb-cycles-infection-curation = 0, on n'autorise qu'une seule infection par an
    let stock-of-sane-patches count patches with [land-cover = 1 and state = 0 and nb-cycles-infection-curation = 0]
    ifelse stock-of-sane-patches > nb-of-crop-patches-to-infect-at-each-tick 
    [
      ; avec la condition nb-cycles-infection-curation = 0, on n'autorise qu'une seule infection par an
      ask n-of nb-of-crop-patches-to-infect-at-each-tick patches with [land-cover = 1 and state = 0 and nb-cycles-infection-curation = 0][set next-state 1]
    ]
    [
      ; si le nb de crops pas encore infectés une seule fois est < au nb de patchs à infecter, on les infecte tous (la condition nb-cycles = 0 est importante alors pour ne pas compter les patchs soignés précédemment)
      ; avec la condition nb-cycles-infection-curation = 0, on n'autorise qu'une seule infection par an
      ask patches with [land-cover = 1 and state = 0 and nb-cycles-infection-curation = 0][set next-state 1]
    ] 
  ]
  
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; between date-to-flee and overwintering

; no new infections

to update-crop-patches-between-date-to-flee-and-overwintering
  
  ;;; update occupied by juveniles
  ask patches with [land-cover = 1 and state = 4]
  [
    set time-since-infection time-since-infection + 1 ; utilité ?
    set juvenile-occupation juvenile-occupation + 1
    if juvenile-occupation = duration-juvenile-occupation [set next-state 0]
  ]

  ;;; transitions
  transitions-between-states-for-crop-patches-between-date-to-flee-and-overwintering

  ;;; coloration
  ask patches with [land-cover = 1 and state = 0] [set pcolor yellow]
  
end

;;;;;;;;;;;; transitions between-date-to-flee-and-overwintering

to transitions-between-states-for-crop-patches-between-date-to-flee-and-overwintering
  
  ;;; occupied by juveniles -> sane
  ask patches with [land-cover = 1 and state = 4 and next-state = 0]
  [
    
    ; update crop patches attributes for regulation service    
    update-crop-loss-with-control-for-this-cycle-of-infection
    update-for-this-crop-patch-total-crop-loss-for-a-season
    set nb-cycles-infection-curation nb-cycles-infection-curation + 1
        
    ; update state and attributes for a new cycle
    set state 0
    set next-state 0
    
    set predator-presence FALSE
    
    set infection-date FALSE
    set time-since-infection FALSE
    
    set latent-period-duration FALSE
    
    set date-of-predators-arrival FALSE
    set first-adult-predator-arrived FALSE
    set time-for-crop-loss 0

    set duration-before-eggs FALSE
    set juvenile-occupation FALSE
    
    set duration-juvenile-occupation FALSE
    set adult-occupation FALSE
    
    set crop-loss-without-control-for-this-cycle-of-infection 0
    set crop-loss-with-control-for-this-cycle-of-infection 0
   
    ; convert juvenile-predators into adult-predators
    ask juvenile-predators-here [die]
    birth-adult-predators
  ]
  
end


