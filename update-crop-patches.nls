globals [
  
  nb-infected-visited-per-year
  
]

to update-crop-patches
  
  ; update predators-presence
  ask patches with [(count adult-predators-here = 0) and (count juvenile-predators-here = 0)][set predator-presence FALSE]
  ask patches with [(count adult-predators-here > 0) or (count juvenile-predators-here > 0)][set predator-presence TRUE]

  ; update age for all patches
  ask patches [set age date]

  ; possibility to choose three pattern-infection methods (see below)
  pattern-infection-2

  ; update latent
  ask patches with [land-cover = 1 and state = 1]
  [
    set time-since-infection time-since-infection + 1
    set time-for-crop-loss time-for-crop-loss + 1
    if time-since-infection = latent-period-duration [set next-state 2]
  ]

  ; update attractive without / with presence of adult-predators

  ask patches with [land-cover = 1 and state = 2 and predator-presence = FALSE]
  [
    set time-since-infection time-since-infection + 1 ; utilité ? ; var
    set time-for-crop-loss time-for-crop-loss + 1
  ]

  ask patches with [land-cover = 1 and state = 2 and predator-presence = TRUE]
  [
    ;
    set first-adult-predator-arrived [who] of one-of adult-predators-here
    
    set time-since-infection time-since-infection + 1 ; utilité ?
    set next-state 3
  ]

  ; update occupied by adults
  ask patches with [land-cover = 1 and state = 3 and predator-presence = TRUE]
  [
    
    
    set time-since-infection time-since-infection + 1 ; utilité ?
    set adult-occupation adult-occupation + 1
    if adult-occupation = duration-before-eggs 
    [
      if random-float 1 <= proba-birth-juvenile-predators ; taux de natalité des adult-predators
      [set next-state 4]
    ]
  ]

  ; update occupied by juveniles
  ask patches with [land-cover = 1 and state = 4]
  [
    set time-since-infection time-since-infection + 1 ; utilité ?
    set juvenile-occupation juvenile-occupation + 1
    if juvenile-occupation = duration-juvenile-occupation [set next-state 0]
  ]

  ; transitions
  transitions-between-states-for-crop-patches

  ; coloration
  ask patches with [land-cover = 1 and state != 0][set pcolor scale-color blue state 4 1]
  ask patches with [land-cover = 1 and state = 0] [set pcolor yellow]

end

;;;

to transitions-between-states-for-crop-patches
  
  ; sane -> latent
  ask patches with [land-cover = 1 and state = 0 and next-state = 1]
  [
    ; update state and assign attributes
    set state 1
    set infection-date age
    set time-since-infection 0
    set latent-period-duration 6 ;+ random 2 ; 6,7,8
    
    ; write distribution of infection events
    write-inf-event-file
        
    ; if first infection, compute the value crop-loss-1st-infection = theoretical crop-loss without any regulation
    ;if crop-loss-1st-infection = 0
    ;[set crop-loss-1st-infection (1 - (infection-date / length-season) ^ gamma-without-CBC)]
    
    update-crop-loss-without-control-for-this-cycle-of-infection
    if nb-cycles-infection-curation = 0 [update-crop-loss-1st-infection]   
  ]

  ; latent -> attractive
  ask patches with [land-cover = 1 and state = 1 and next-state = 2]
  [
    set state 2
  ]

  ; attractive -> occupied by adults
  ask patches with [land-cover = 1 and state = 2 and next-state = 3]
  [
    ; update state and assign attributes
    set state 3
    set adult-occupation 0
    set duration-before-eggs 6 ;+ random 2 ; 6,7,8
    
    ; write distribution of predators-arrival events
    write-pred-event-file
  ]

  ; occupied by adults -> occupied by juveniles
  ask patches with [land-cover = 1 and state = 3 and next-state = 4]
  [
    ; update state and assign attributes
    set state 4
    birth-juvenile-predators
    set juvenile-occupation 0
    set duration-juvenile-occupation 15 ;+ random 6
  ]

  ; occupied by juveniles -> sane
  ask patches with [land-cover = 1 and state = 4 and next-state = 0]
  [
    
    ; update crop patches attributes for regulation service    
    update-crop-loss-with-control-for-this-cycle-of-infection
    update-total-crop-loss-for-a-season
    
    ; increment var total nb of crop patches infected and visited
    set nb-infected-visited-per-year nb-infected-visited-per-year + 1
    
    ; increment attribute of this patch to count cycles of SIS
    set nb-cycles-infection-curation nb-cycles-infection-curation + 1
    
    ; update state and attributes
    set state 0
    set next-state 0
    
    set predator-presence FALSE
    set first-adult-predator-arrived FALSE

    set infection-date FALSE
    set time-since-infection 0
    set time-for-crop-loss 0
    set latent-period-duration 0
    
    set adult-occupation 0
    set duration-before-eggs 0
    
    set juvenile-occupation 0
    set duration-juvenile-occupation 0

    set crop-loss-without-control-for-this-cycle-of-infection 0
    set crop-loss-with-control-for-this-cycle-of-infection 0
   
    ; convert the juvenile turtle into an adult turtle
    ask juvenile-predators-here [die]
    birth-adult-predators
  ]
end

;;; rules for pattern-infection

; beginning of infection in the year: var.?

; assumptions:
; infection depends on:
; exogenous infection = basic rate and spatially randomized
; infection state of the landscape at each tick? = when the number of infected patches increases, we can think that the virulence increases too => regulation by predators is important
; influence of SNH patches as sources of pests? => relation with the number of SNH patches?
; transition between years: is the infection-rate depends on the previous nb of infected patches?

; à chaque tick, on infecte un nb. de crops égal à infection-rate : ex. si inf-rate = 4 alors 4 crops sont infectés à chaque tick
to pattern-infection
  
  if (ticks mod infection-pattern-frequency) = 0 ; pattern-infection occurs every 3 ticks (not every tick)
  [
    ; each tick, a certain number of sane patches are infected
    ; avec la condition nb-cycles-infection-curation = 0, on n'autorise qu'une seule infection par an
    let stock-of-sane-patches count patches with [land-cover = 1 and state = 0 and nb-cycles-infection-curation = 0]
    ifelse stock-of-sane-patches > infection-rate 
    [
      let number-of-sane-patches-to-infect infection-rate
      ; avec la condition nb-cycles-infection-curation = 0, on n'autorise qu'une seule infection par an
      ask n-of number-of-sane-patches-to-infect patches with [land-cover = 1 and state = 0 and nb-cycles-infection-curation = 0][set next-state 1]
    ]
    [
      let number-of-sane-patches-to-infect stock-of-sane-patches
      ; avec la condition nb-cycles-infection-curation = 0, on n'autorise qu'une seule infection par an
      ask n-of number-of-sane-patches-to-infect patches with [land-cover = 1 and state = 0 and nb-cycles-infection-curation = 0][set next-state 1]
    ] 
  ]
  
end


; à chaque tick, on infecte un % du stock de crops : ex. si inf-rate = 4, alors 4% du stock initial de crops est infecté à chaque tick
to pattern-infection-2
  
  let nb-of-crop-patches-to-infect-at-each-tick precision (infection-rate / 100 * number-of-crop-patches) 0 ; precision => integer
  
  if (ticks mod infection-pattern-frequency) = 0 ; pattern-infection occurs every 3 ticks (not every tick)
  [
    ; each tick, a certain number of sane patches are infected
    ; avec la condition nb-cycles-infection-curation = 0, on n'autorise qu'une seule infection par an
    let stock-of-sane-patches count patches with [land-cover = 1 and state = 0 and nb-cycles-infection-curation = 0]
    ifelse stock-of-sane-patches > nb-of-crop-patches-to-infect-at-each-tick 
    [
      ; avec la condition nb-cycles-infection-curation = 0, on n'autorise qu'une seule infection par an
      ask n-of nb-of-crop-patches-to-infect-at-each-tick patches with [land-cover = 1 and state = 0 and nb-cycles-infection-curation = 0][set next-state 1]
    ]
    [
      ; si le nb de crops pas encore infectés une seule fois est < au nb de patchs à infecter, on les infecte tous (la condition nb-cycles = 0 est importante alors pour ne pas compter les patchs soignés précédemment)
      ; avec la condition nb-cycles-infection-curation = 0, on n'autorise qu'une seule infection par an
      ask patches with [land-cover = 1 and state = 0 and nb-cycles-infection-curation = 0][set next-state 1]
    ] 
  ]
  
end


;;;;;;;;;;;;;;;;;;;;
; => à checker, mais paraît ok

; à la fin de la simulation, le nb de crops infectés correspond à inf-rate : 
; ex. si inf-rate = 4, alors à la fin de la simulation 4% des crops ont été infectés (par rapport au stock inital de crops)
to pattern-infection-3
  
  let final-nb-of-crop-patches-to-infect precision (infection-rate / 100 * number-of-crop-patches) 0
  
  ; 150 ticks -> final-nb-of-crop-patches-to-infect
  ; 1 tick -> nb of crops to infect at each tick
  
  ; ceiling pour arrondir à l'entier supérieur (au cas où le nb de patchs à infecter à chaque tick est compris entre 0 et 1, et / ou décimal)
  let nb-of-crop-patches-to-infect-at-each-tick ceiling (final-nb-of-crop-patches-to-infect / (date-to-flee / infection-pattern-frequency))
  
  if (ticks mod infection-pattern-frequency) = 0 ; pattern-infection occurs every 3 ticks (not every tick)
  [
    ; comme on a potentiellement arrondi le nb de crops à infecter à chaque tick
    ; on risque de dépasser le nb final de crops à infecter
    ifelse (count patches with [land-cover = 1 and state != 0] + count patches with [state = 0 and nb-cycles-infection-curation > 0] + nb-of-crop-patches-to-infect-at-each-tick) < final-nb-of-crop-patches-to-infect
    [
      ; each tick, a certain number of sane patches are infected
      ; avec la condition nb-cycles-infection-curation = 0, on n'autorise qu'une seule infection par an
      let stock-of-sane-patches count patches with [land-cover = 1 and state = 0 and nb-cycles-infection-curation = 0]
      ifelse stock-of-sane-patches > nb-of-crop-patches-to-infect-at-each-tick 
      [
        ; avec la condition nb-cycles-infection-curation = 0, on n'autorise qu'une seule infection par an
        ask n-of nb-of-crop-patches-to-infect-at-each-tick patches with [land-cover = 1 and state = 0 and nb-cycles-infection-curation = 0][set next-state 1]
      ]
      [
        ; si le nb de crops pas encore infectés une seule fois est < au nb de patchs à infecter, on les infecte tous (la condition nb-cycles = 0 est importante alors pour ne pas compter les patchs soignés précédemment)
        ; avec la condition nb-cycles-infection-curation = 0, on n'autorise qu'une seule infection par an
        ask patches with [land-cover = 1 and state = 0 and nb-cycles-infection-curation = 0][set next-state 1]
      ] 
    ]
    [ask n-of (final-nb-of-crop-patches-to-infect - (count patches with [land-cover = 1 and state != 0] + count patches with [state = 0 and nb-cycles-infection-curation > 0])) patches with [land-cover = 1 and state = 0 and nb-cycles-infection-curation = 0][set next-state 1]]
  ]
  
end
